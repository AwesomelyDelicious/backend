plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.2'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.sungkyul.decemberproject'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	asciidoctorExt
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	runtimeOnly 'mysql:mysql-connector-java'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

}
ext {
	set('snippetsDir', file('build/generated-snippets'))
}

task createDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/static")
}


asciidoctor {
	configurations 'asciidoctorExt'
	baseDirFollowsSourceFile()
	inputs.dir snippetsDir
	dependsOn test
}
asciidoctor.doFirst {
	delete file('src/main/resources/static/docs')
}
bootJar {
	dependsOn createDocument
	from ("${asciidoctor.outputDir}"){
		into 'static/docs'
	}
}
tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}
